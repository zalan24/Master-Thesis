

--- vs ---
#version 430 core

// TODO put in UBO
uniform mat4 PVM;
uniform mat4 model;

in vec3 vPos;
in vec3 vNorm;
in vec3 vColor;
in vec2 vTex;
in vec4 vBoneIds;
in vec4 vBoneWeights;

out vec2 texcoord;
out vec3 normal;
out vec3 color;

layout (std430, binding=0) readonly restrict buffer BonesBlock
{
  mat4 bones[];
};

void main()
{
  ivec4 boneIds = ivec4(vBoneIds);
  mat4 boneTm =
    bones[boneIds.x] * vBoneWeights.x +
    bones[boneIds.y] * vBoneWeights.y +
    bones[boneIds.z] * vBoneWeights.z +
    bones[boneIds.w] * vBoneWeights.w;
  gl_Position = PVM * (boneTm * vec4(vPos, 1.0));
  normal = normalize((model * (boneTm * vec4(vNorm, 0.0))).xyz);
  texcoord = vTex;
  color = vColor;
}

--- ps ---

#version 430 core

uniform vec3 lightDir;
uniform vec3 lightColor;
uniform vec3 ambientColor;
uniform float alphaClipping;

in vec2 texcoord;
in vec3 normal;
in vec3 color;

uniform sampler2D diffuse_tex;

void main()
{
    vec4 diffTex = texture( diffuse_tex, texcoord );

    vec3 albedo = clamp(diffTex.rgb + color, vec3(0), vec3(1));
    float alpha = diffTex.a;

    if (alpha < alphaClipping)
        discard;

    float cosTerm = clamp(dot(normal, -lightDir), 0, 1);
    vec3 diffuse = albedo * (cosTerm * lightColor + ambientColor);
    gl_FragColor = vec4(diffuse, 1);
}

---